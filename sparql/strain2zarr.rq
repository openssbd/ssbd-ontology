PREFIX ssbd: <http://ssbd.riken.jp/ontology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo:  <http://purl.obolibrary.org/obo/>

SELECT ?dataset ?bs ?title ?methodIRI ?methodLabel ?zarr ?vizarr
WHERE {
  # --- strain filter (C57BL/6J) ---
  ?bs ssbd:is_about_strain <https://www.jax.org/strain/000664> .

  # --- Dataset â†” Biosample ---
  ?dataset ssbd:has_biosample_information ?bs ;
           ssbd:has_dataset_title         ?title .
  
    # --- Imaging method  ---
  ?dataset ssbd:has_imaging_method_total_info  ?imNode .
  ?imNode  ssbd:has_imaging_method_recorded_type ?methodIRI .
  OPTIONAL { ?methodIRI rdfs:label ?methodLabel }
  
   # --- NGFF ---
  OPTIONAL {
    ?dataset ssbd:has_ome_zarr_information ?ngff .
    OPTIONAL { ?ngff ssbd:has_s3_endpoint ?zarr }
    OPTIONAL { ?ngff ssbd:has_vizarr_url  ?vizarr }
  }

}
ORDER BY ?methodIRI ?dataset